import glob
import sys

from pandas import concat, read_excel


## GLOBALS ---------------------------------------------------------------------

# List the datasets to be processed
DATASETS = glob.glob("data/org/amph/*/hdr.adf") + \
           glob.glob("data/org/bird/*/hdr.adf") + \
           glob.glob("data/org/mam/*/hdr.adf") + \
           glob.glob("data/org/rept/*/hdr.adf")

## RULES -----------------------------------------------------------------------

rule all:
	input: expand("data/european_tetrapods/{taxon}", taxon=["amphibians", "birds", "mammals", "reptiles"])

# Rule to checkt the extents of tranlsated bird files.
rule check_extents:
    input:
        DATASETS
    output:
        temp(touch("data/org/extent.ok"))
    log:
        "log/extents.log"
    message: "Checking raster extents..."
    script:
        "scripts/check_extents.py"

# Rule to convert the Excel file containing the species names into CSV.
rule convert_spp_names:
    input:
        excel_file="data/org/Codes.xlsx"
    output:
        csv_file="data/european_tetrapods/spp_codes.csv"
    log:
        "log/spp_convert.log"
    message: "converting {input} to CSV."
    run:
        # Load data
        spp_names = read_excel(input.excel_file, sheetname="Foglio1")
        # Fix column headers
        spp_names.columns = ["code", "class", "species"]
        # Write output
        spp_names.to_csv(output.csv_file, index=False)

# Rule to translate a filtered set of bird species distributions from AIG to
# GeoTIFFs. File names will be replaced with the species name.
rule translate_spp_rasters:
    input:
        datasets=DATASETS,
        extents_ok=rules.check_extents.output[0],
        spp_names_file=rules.convert_spp_names.output.csv_file
    output:
        output_dirs=rules.all.input,
        data_manifest="data/european_tetrapods/data_manifest.yml"
    log:
        "log/translate_rasters.log"
    message: "Translating files to GeoTIFF"
    script:
        "scripts/translate_rasters.py"
